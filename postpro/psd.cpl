!
! This program computes the power spectral density 
! and cross spectral density of:
!
! (uu, vv, ww, uv, vw, uw)
!
! This program IS parallel
!

gamma=0
outinterv=0
USE ../dnsdata
USE ../dnsdirect
!USE rtchecks
USE ../tke/convenience.cpl

! Parameters
INTEGER nfmin=1,nfmax=31, nftot
STRING path="./postpro-fld/"
!------------
nftot=nfmax-nfmin+1

! Definitions
MEANTERMS = STRUCTURE(REAL U,W)
SPECTRUM=STRUCTURE(REAL uu,vv,ww,uv,uw,vw)

! Array declarations
ARRAY(nyl-2..nyh+2,0..nx,-nz..nz) OF SPECTRUM psd=0
ARRAY(nyl-2..nyh+2) OF MEANTERMS meandata=0
POINTER TO STORED STRUCTURE[
        ARRAY(-1..ny+1,0..nx,-nz..nz) OF SPECTRUM psd_image
] psd_file

! Compute the average field
LOOP FOR n=nfmin TO nfmax
  STRING field_name = WRITE(path'Field'n'.fld')
  IF NOT first THEN READ FROM prev
  diskfield=OPEN(field_name)
  WITH diskfield,meandata(*):
    U=~+uavimage(nyl-2..nyh+2)
    W=~+wavimage(nyl-2..nyh+2)
  CLOSE diskfield
  IF NOT last THEN WRITE TO next
REPEAT 
DO WITH meandata(iy): U=U/nftot; W=W/nftot; FOR ALL iy 

! Main loop on files
LOOP files FOR n=nfmin TO nfmax
    ! Read field
    STRING field_name = WRITE(path'Field'n'.fld')
    read_field_parallel(field_name)
    WITH V(0,0,*),meandata(*): u=~-U; w=~-W
    ! Compute PSD and CSD
    IF has_terminal THEN WRITE "Computing PSD and CSD"
    DO
      DO WITH psd(iy,ix,iz),V(ix,iz,iy):  uu=~+2*NORM[u]; vv=~+2*NORM[v]; ww=~+2*NORM[w]; uv=~+2*[u|v].REAL; uw=~+2*[u|w].REAL; vw=~+2*[v|w].REAL FOR ix=1 TO nx
         WITH psd(iy,0,iz),V(0,iz,iy):   uu=~+  NORM[u]; vv=~+  NORM[v]; ww=~+  NORM[w]; uv=~+  [u|v].REAL; uw=~+  [u|w].REAL; vw=~+  [v|w].REAL
    FOR ALL iy,iz 
REPEAT files

! Average
WITH psd(*,*,*): uu=~/nftot; vv=~/nftot; ww=~/nftot; uv=~/nftot; uw=~/nftot; vw=~/nftot;

! Save to binary
IF has_terminal THEN WRITE "Saving to psd.bin"
IF NOT first THEN READ FROM prev
	psd_file = OPEN('psd.bin')
	WITH psd_file: psd_image(miny..maxy) = psd(miny..maxy)
	CLOSE psd_file
IF NOT last THEN WRITE TO next

! Be polite and say goodbye
WRITE "Goodbye, man!"
